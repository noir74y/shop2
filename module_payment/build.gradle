import org.openapitools.generator.gradle.plugin.tasks.GenerateTask

plugins {
    id 'org.openapi.generator'
}

dependencies {
    implementation "org.springdoc:springdoc-openapi-starter-webflux-ui:${springdocOpenapiStarterWebFluxUi}"
}

openApiGenerate {
    generatorName.set("spring")
    inputSpec.set(layout.projectDirectory.file("src/main/resources/api-spec.yaml").toString())
    outputDir.set(layout.buildDirectory.dir("generated/openapi-server").get().toString())
    modelPackage.set("ru.noir74.payment.models.dto")
    apiPackage.set("ru.noir74.payment.controllers")
    invokerPackage.set("ru.noir74.payment.clients")
    configOptions.set([
            hideGenerationTimestamp: "true",
            requestMappingMode: "controller",
            interfaceOnly: "true",
            library: "spring-boot",
            reactive: "true",
            useSpringBoot3: "true",
            useJakartaEe: "true",
            useTags: "false",
            dateLibrary: "java8",
            openApiNullable: "false",
            serializableModel: "true",
            returnSuccessCode: "true"
    ])
}

tasks.register('buildClient', GenerateTask) {
    generatorName.set("java")
    inputSpec.set(layout.projectDirectory.file("src/main/resources/api-spec.yaml").toString())
    outputDir.set(layout.buildDirectory.dir("generated/openapi-client").get().toString())
    apiPackage.set("ru.noir74.shop.client.api")
    invokerPackage.set("ru.noir74.shop.client.clients")
    configOptions.set([
            hideGenerationTimestamp: "true",
            library                : "webclient",
            useJakartaEe           : "true",
            useTags                : "false",
            openApiNullable        : "false",
            serializableModel      : "true",
            skipDefaultInterface   : "true",
            generateModels         : "false"
    ])
}

tasks.named('compileJava') {
    dependsOn tasks.openApiGenerate
    dependsOn tasks.buildClient
}

sourceSets {
    main {
        java {
            srcDirs = [
                    'src/main/java',
                    "${buildDir}/generated/openapi-server/src/main/java",
                    "${buildDir}/generated/openapi-client/src/main/java"
            ]
        }
    }
}